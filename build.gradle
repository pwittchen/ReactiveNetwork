// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'io.codearte.nexus-staging'

ext {
  minSdkVersion = 9
  minSdkVersionApps = 23
  compileSdkVersion = 28
  buildToolsVersion = '28.0.3'
  gradleVersion = '4.6'
  kotlinVersion = '1.3.31'
  detektVersion = '1.0.0.RC6-1'
}

ext.deps = [rxjava2           : 'io.reactivex.rxjava2:rxjava:2.2.8',
            rxandroid2        : 'io.reactivex.rxjava2:rxandroid:2.1.1',
            annotation        : 'androidx.annotation:annotation:1.1.0',
            appcompat         : 'androidx.appcompat:appcompat:1.0.2',
            junit             : 'junit:junit:4.12',
            truth             : 'com.google.truth:truth:0.42',
            robolectric       : 'org.robolectric:robolectric:4.1',
            mockitocore       : 'org.mockito:mockito-core:2.23.4',
            nullaway          : 'com.uber.nullaway:nullaway:0.5.4',
            errorprone        : 'com.google.errorprone:error_prone_core:2.3.2',
            ktlintgradle      : 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:4.1.0',
            kotlinstdlib      : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            kotlingradleplugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
            detekt            : "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"]

buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.0'
    classpath('com.dicedmelon.gradle:jacoco-android:0.1.4') {
      exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.19'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.12.0"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

nexusStaging {
  packageGroup = GROUP //optional if packageGroup == project.getGroup()
  stagingProfileId = "9add401d06ecc9" //when not defined will be got from server using "packageGroup"
  username = getRepositoryUsername()
  password = getRepositoryPassword()
}