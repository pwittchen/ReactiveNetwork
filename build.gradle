// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'io.codearte.nexus-staging'

ext {
  minSdkVersion = 14
  minSdkVersionApps = 23
  compileSdkVersion = 28
  buildToolsVersion = '28.0.3'
  gradleVersion = '4.6'
  kotlinVersion = '1.3.72'
  detektVersion = '1.0.0.RC6-1'
}

ext.deps = [rxjava2           : 'io.reactivex.rxjava2:rxjava:2.2.19',
            rxandroid2        : 'io.reactivex.rxjava2:rxandroid:2.1.1',
            annotation        : 'androidx.annotation:annotation:1.1.0',
            appcompat         : 'androidx.appcompat:appcompat:1.1.0',
            junit             : 'junit:junit:4.13',
            truth             : 'com.google.truth:truth:1.0.1',
            robolectric       : 'org.robolectric:robolectric:4.3.1',
            mockitocore       : 'org.mockito:mockito-core:3.3.3',
            nullaway          : 'com.uber.nullaway:nullaway:0.7.9',
            errorprone        : 'com.google.errorprone:error_prone_core:2.3.4',
            nopen             : 'com.jakewharton.nopen:nopen-annotations:1.0.1',
            nopenchecker      : 'com.jakewharton.nopen:nopen-checker:1.0.1',
            kotlinstdlib      : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            kotlingradleplugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
            detekt            : "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"]

buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.3'
    classpath('com.hiya:jacoco-android:0.2') {
      exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.21'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

nexusStaging {
  packageGroup = GROUP //optional if packageGroup == project.getGroup()
  stagingProfileId = "9add401d06ecc9" //when not defined will be got from server using "packageGroup"
  username = getRepositoryUsername()
  password = getRepositoryPassword()
}